# Use `just <recipe>` to run a recipe
# https://just.systems/man/en/

# Aliases

# Open a terminal with the cabinet-app session
[group('dev')]
term-run:
    zellij --layout term.kdl attach {{ zellijSession }} -c

# Kill the cabinet-app session
[group('dev')]
term-kill:
    -zellij delete-session {{ zellijSession }} -f

# Kill and run a terminal with the cabinet-app session
[group('dev')]
term: term-kill term-run

# Open a browser with the application (http/https)
[group('dev')]
open-browser:
    curl -s http://127.0.0.1:8001 > /dev/null && xdg-open http://127.0.0.1:8001 || xdg-open https://127.0.0.1:8001

# Interactive npm watch script selector
[group('dev')]
watch:
    #!/usr/bin/env bash
    set -euo pipefail

    # Define the watch scripts with descriptions
    watch_scripts=(
        "watch: Whitelabel app"
        "watch-custom: TU Graz app"
    )

    # Use fzf to select a script
    selected_script=$(printf '%s\n' "${watch_scripts[@]}" | fzf \
        --height 40% \
        --layout=reverse \
        --border \
        --prompt='Select NPM watch script > ' \
        --preview='echo "Will run: npm run $(echo {} | cut -d: -f1)"' \
        --preview-window=up:1 \
        | cut -d: -f1)

    # Check if a script was selected
    if [[ -n "$selected_script" ]]; then
        # Get the full line for the selected script
        full_text=$(printf '%s\n' "${watch_scripts[@]}" | grep "^$selected_script:")

        # Set Zellij pane name
        if command -v zellij &> /dev/null; then
            zellij action rename-pane "$full_text"
        fi

        echo "Running: npm run $selected_script"
        npm run "$selected_script"
    else
        echo "No script selected. Exiting."
        exit 1
    fi

# Remove all node-modules directories in the toolkit
[group('dev')]
remove-toolkit-node-modules:
    rm ./vendor/toolkit/node_modules -Rf
    rm ./vendor/toolkit/packages/*/node_modules -Rf

# Remove all node-modules directories in the vendor folder recursively
[group('dev')]
remove-vendor-node-modules:
    find ./vendor -type d -name 'node_modules' -prune -exec rm -rf '{}' +

# Run checker
[group('linter')]
check:
    npm run check

# Run fixer
[group('linter')]
fix:
    npm run fix

# Checkout main branch in a directory after checking for uncommitted changes
[group('git')]
checkout-main dir:
    #!/usr/bin/env bash
    set -euo pipefail

    if [ ! -d "{{dir}}" ]; then
        echo "Error: Directory '{{dir}}' does not exist"
        exit 1
    fi

    # Check if it's a git repository (.git can be a directory or a file for submodules)
    if [ ! -e "{{dir}}/.git" ]; then
        echo "Error: '{{dir}}' is not a git repository"
        exit 1
    fi

    cd "{{dir}}"

    # Check for uncommitted changes (staged files)
    if ! git diff --cached --quiet; then
        echo "Error: There are staged changes in {{dir}}"
        echo "Please commit or unstage them first"
        git diff --cached --name-status
        exit 1
    fi

    # Check for unstaged changes (modified files)
    if ! git diff --quiet; then
        echo "Error: There are unstaged changes in {{dir}}"
        echo "Please commit or stash them first"
        git diff --name-status
        exit 1
    fi

    # Check for untracked files
    if [ -n "$(git ls-files --others --exclude-standard)" ]; then
        echo "Error: There are untracked files in {{dir}}"
        echo "Please handle them first"
        git ls-files --others --exclude-standard
        exit 1
    fi

    echo "Repository is clean, checking out main branch..."
    git checkout main
    echo "Successfully checked out main branch in {{dir}}"

# Checkout main branch for toolkit
[group('git')]
checkout-main-toolkit:
    just checkout-main vendor/toolkit
